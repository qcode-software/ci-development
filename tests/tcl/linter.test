package require tcltest
namespace import ::tcltest::test
namespace import ::tcltest::cleanupTests
namespace import ::tcltest::testConstraint

set files [lsort [glob -nocomplain "~/ci-development/tcl/*.tcl"]]

foreach file $files {
    source $file
}

set setup {
    set test_file "~/ci-development/tests/tcl/files/linter_test.tcl"
}

test linter_cat-1.0 \
    {Get the contets of a file.} \
    -setup $setup \
    -body {
        set contents [linter_cat $test_file]

        return [expr { [string length $contents] > 0 }]
    } \
    -result 1

test linter_report_files_over_length-1.0 \
    {Test that long files are reported.} \
    -setup $setup \
    -body {
        set report [linter_report_files_over_length [list $test_file] 1]

        return [expr { [string length $report] > 0 }]
    } \
    -result 1

test linter_count_files_over_length-1.0 \
    {Count files over a given number of lines.} \
    -setup $setup \
    -body {
        return [linter_count_files_over_length [list $test_file] 1]
    } \
    -result 1

test linter_report_lines_over_length-1.0 \
    {Test long lines are reported for a given file.} \
    -setup $setup \
    -body {
        set report [linter_report_lines_over_length [list $test_file] 90]

        return [expr { [string length $report] > 0 }]
    } \
    -result 1

test linter_count_lines_over_length-1.0 \
    {Count the lines over a given length.} \
    -setup $setup \
    -body {
        return [linter_count_lines_over_length [list $test_file] 90]
    } \
    -result 2

test linter_report_procs_without_filename_prefix-1.0 \
    {Test proc names not prefixed with the file name are reported.} \
    -setup $setup \
    -body {
        set report [linter_report_procs_without_filename_prefix [list $test_file]]

        return [expr { [string length $report] > 0 }]
    } \
    -result 1

test linter_count_procs_without_filename_prefix-1.0 \
    {Count procs not prefixed with the file name.} \
    -setup $setup \
    -body {
        return [linter_count_procs_without_filename_prefix [list $test_file]]
    } \
    -result 2

test linter_proc_names_parse-1.0 \
    {Test that proc names are parsed from a string.} \
    -body {
        set proc_names [linter_proc_names_parse {
            proc proc_names_test_1 {args} {
                return "Hello World"
            }

            proc ::proc_names_test_2 {arg1 arg2} {
                return "Hello World"
            }

            proc testing::proc_names_test_3 {} {
                return "Hello World"
            }

            proc testing::names::proc_names_test_4 {} {
                return "Hello World"
            }
        }]

        return [expr {
                      [llength $proc_names] == 4
                      && "proc_names_test_1" in $proc_names
                      && "proc_names_test_2" in $proc_names
                      && "proc_names_test_3" in $proc_names
                      && "proc_names_test_4" in $proc_names
                  }]
    } \
    -result 1

test linter_tcl_commands-1.0 \
    {Get the Tcl commands from a string.} \
    -setup $setup \
    -body {
        set test {
            proc test_1 {args} {
                # This is a test proc.
                return 1
            }

            set foo "bar"
        }

        set commands [linter_tcl_commands $test]

        set command1 [lindex $commands 0]
        set command2 [lindex $commands 1]

        if { [lindex $command1 0] ne "proc" } {
            error "Expected proc but got [lindex $command1 0]."
        }

        if { [lindex $command1 1] ne "test_1" } {
            error "Expected test_1 but got [lindex $command1 1]."
        }

        if { [llength [lindex $command1 2]] != 1
             || [lindex [lindex $command1 2] 0] ne "args" } {
            error "Expected {args} but got [lindex $command1 2]."
        }

        if { [llength [split [string trim [lindex $command1 3]] "\n"]] != 2 } {
            error "Expected 2 lines in proc test_1 body but got\
                   [string trim [lindex $command1 3]]."
        }

        if { $command2 ne {set foo "bar"} } {
            error "Expected {set foo "bar"} but got $command2."
        }

        return 1
    } \
    -result 1

test linter_sql_query_indices-1.0 \
    {Get the indices of queries in a string.} \
    -setup $setup \
    -body {
        set string {
            set query1 {
                select
                *

                from
                test
            }

            set query2 {
                insert into test
                (a, b, c)
                values
                (1, 2, 3)
            }

            set query3 {
                update
                test
                set
                a = 1
                where
                b = 2
            }

            set query4 {delete from test}
        }

        return [linter_sql_query_indices $string] 
    } \
    -result {{24 122} {148 270} {296 440} {466 483}}

test linter_sql_queries_length-1.0 \
    {Get the combined length of queries in a string.} \
    -setup $setup \
    -body {
        set string {
            set query1 {
                select
                *

                from
                test
            }

            set query2 {
                insert into test
                (a, b, c)
                values
                (1, 2, 3)
            }

            set query3 {
                update
                test
                set
                a = 1
                where
                b = 2
            }

            set query4 {delete from test}
        }
        return [linter_sql_queries_length $string]
    } \
    -result {22}

test linter_proc_lengths-1.0 \
    {Get body length of procs in a string.} \
    -setup $setup \
    -body {
        set test {
            proc test_1 {args} {
                # This is a test proc.
                return 1
            }

            proc test_2 {} {
                set foo 1
                set bar 2

                return [expr {$foo + $bar}]
            }

            namespace eval tests {

                namespace export test_3

                namespace ensemble create

                proc test_3 {args} {
                    #| Ensemble proc.

                    return "a"
                }
            }

            namespace eval test_vars {
                set vars [list]
                lappend vars \
                    [dict create \
                         a 1 \
                         b 2 \
                         c 3] \
                    [dict create \
                         a 4 \
                         b 5 \
                         c 6]

                proc test_4 {args} {
                    return 4
                }
            }

            proc test_5 {} {
                return {select a, b, c from test where id = 123}
            }

            proc test_6 {} {
                db_1row {
                    select
                    a,
                    b,
                    c

                    from test
                }

                db_dml {
                    update test
                    set a = :c
                    where id = :b
                }

                db_dml {
                    insert into test
                    (a, b, c)
                    values
                    (1, 2, 3)
                }

                db_dml {
                    delete from test
                }

                return 1
            }
        }

        return [linter_proc_lengths $test]
    } \
    -result {test_1 2 test_2 4 test_3 3 test_4 1 test_5 1 test_6 9}

test linter_report_procs_over_length-1.0 \
    {Test that procs with bodies over a specified number of lines are reported.} \
    -setup $setup \
    -body {
        set report [linter_report_procs_over_length [list $test_file] 3]

        return [expr { [string length $report] > 0 }]
    } \
    -result 1

test linter_count_procs_over_length-1.0 \
    {Count procs with bodies over a specified number of lines.} \
    -setup $setup \
    -body {
        return [linter_count_procs_over_length [list $test_file] 3]
    } \
    -result 1

cleanupTests
